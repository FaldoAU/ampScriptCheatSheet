// Dave's AMPScript cheat sheet.

// AMPScript is ExactTarget (SFDC Marketing Cloud)'s server side scripting language.
// It can be a bit of a pig: the docs are numerous and the examples aren't contextual.

// This assumes you don't need hand holding, and just need to get your head around it fast.
// It's also my reference for the snippets I use most frequently (I'm a formatting stickler).

// These examples come from microsites I've written - adapt 'em for use elsewhere.
// Multi line AMPScript %%[contained within delimiters]%% gets interpreted on the server.
// Single line AMPScript %%=can use this style syntax=%% if you want.


// Assign URL key/value pairs to @someVar, eg: et.company.com/form?country=AU&bu=MP
set @country_code = queryParameter("country")
set @business_unit = queryParameter("bu")


// Look up a Data Extension (T-SQL table in SFDC:MC) and put resulting rows into a var. Multiple pairs work.
set @queryResults = LookupRows("DE Name or Key", "search_this_field", @for_this_value)
set @translate = LookupRows("Label Translator DB", "country", @country_code, "other_field", "other_var")


// A one-liner for storing the value in "field_name" of the 1st query result into @some_var.
set @trans_label_form_type = Field(Row(@translate, 1), "form_type")
set @trans_label_last_name = Field(Row(@translate, 1), "last_name")


// There's email and not email versions of insert(), upsert() etc.
// This is the Not An Email version of insertData().
// To do this in an Email, use insertDE(). You might need to specify numFields being in/upserted. Check the docs.
InsertData("Example Contact Form DE", "email_address", @email_address, "first_name", @first_name, "last_name", @last_name)


// Conditional syntax is "if then elseif else endif". output() dumps text.
if rowcount(@translate) == 0 then
  set @translate = LookupRows("Translation DE", "country", "AU", "bu", "some_var")
  
  output(concat("No results for the combination of ", @country_code, " and ", @business_unit " found in the Translation DE. Falling back to defaults. Please check ET's data extensions, your URL and any debug options. If symptoms persist, contact dave@field.bz with the address of this page.<br>"))
endif


// For loop syntax is "for to do next". Before the practical example though:
// BONUS: you can combine AMPScript and client side Javascript if you're careful.
// AMPScript loops will vomit n blocks of Javascript into the browser on load.
// Be careful of variable names, they can get confusing when juggling languages.

// This will to fill an option element (HTML select drop down) with the results of a DB query.
%%[
var @i
for @i=1 to RowCount(@disciplines) do
]%%
  var currentDiscipline = ("%%=v(Field(Row(@disciplines, @i),"discipline"))=%%");
  var Discipline = document.getElementById("Discipline");
  var option = document.createElement("option");
  option.text = current_discipline;
  option.value = current_discipline;
  Discipline.add(option);
%%[
next @i
]%%

// The more elegant way of ding the above is usingwith whichever language poweres your website's SDK.
